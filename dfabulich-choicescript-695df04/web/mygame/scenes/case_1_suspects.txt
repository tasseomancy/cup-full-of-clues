*if skip
	*finish
The door bangs open and Margot staggers in, holding a thick stack of three-ring binders. She piles them onto the conference table and throws herself onto a chair.

“Casey, did Rene thoroughly explain the drill?”
*choice
	#"Yep, I’m ready to go."
		*set resist_colleague -1
		“Good, because we’re in a rush.”
		*goto brief
	#"Yes, but I have a few more questions."
		*set resist_colleague +1
		“No time now, but we’ll talk again later.”
		*goto brief
	#"Mostly, but did you want to add anything?"
		*set boss_regard +1
		“Just remember, don’t mention SEKA to anyone.”
		*goto brief

*label brief
*page_break
“Today, we’re trying to tune SEKA’s ability to understand whether someone is running an illicit business while they’re on the job. SEKA has flagged fifteen Ubiquity employees as behaving suspiciously. We’ll need to investigate them, figure out if they’re doing anything wrong, and then adjust SEKA so it’ll handle this topic better in the future. Each of us will examine five case studies." 

"Take five binders, skim the info, and prioritize the least suspicious people first, since those might be the cases where SEKA made the biggest mistakes. Come back here by 3pm with your decisions on each case, whether you had time to interview everyone or not. We’ll input our corrections into SEKA together.”

*page_break
You start flipping through one of the binders which contains records for [b]Jason L[/b]. Your attention is caught by one phrase in particular:
*fake_choice
	#From a peer review: “He’s got a hair trigger temper, and we’re all walking on eggshells, trying not to set him off.”
		[i]This guy sounds like a real joy to work with. Not.[/i]
	#From a manager evaluation: “He arrives 30-60 minutes late to work, on average, twice a week.”
		[i]Hard to run a secret side-business when you’re on the clock at work if you’re late getting here all the time.[/i]
	#From a web browsing log: “Social media: 30 views, Online dating: 50 views, Forums: 100 views”
		[i]I wonder what kinds of forums he’s looking at.[/i]
	#From an email: “We need to reschedule our raid to start at 11pm, because a couple people need to work late.”
		[i]It’s kind of hard-core to set up your guild raid at 11pm, but not super hard-core if people in your guild are letting work interfere with previously scheduled raids![/i]

*page_break
You look up. Margot and Rene are also flipping through binders. “Any instructions for how should I investigate people?”

“Take 30 minutes to skim the files so you can start with the least suspicious individuals. Since we’re correcting SEKA, we need to be able to tell it how it went wrong. So try to understand why it flagged each person and decide if SEKA was right." 

"You can talk to the people who were flagged, talk to their coworkers, watch them at lunch, or read their email logs for the week - do whatever’s necessary to understand the real explanation for their suspicious behavior. Don’t mention SEKA and get back here by 3pm with all your decisions.”

*page_break
[i]That’s less than 6 minutes to look at each case study, and figure out who to study first! And that leaves about an hour per case for detailed investigation. No wonder Margot was desperate to get a new employee for the team.[/i]

Do you:
*choice
	#Flip through the rest of the case files and prioritize each person. 
		*set diligence +1
		*goto other_files
	#Try using Virtual Teacup to pick the first person to investigate. Then get started right away, to save a little time. 
		*set boss_regard -1

		You launch Virtual Teacup. [i]Should I start with Jason L?[/i]
		
		The tea leaves look like a perched crow. 
		
		*if soul > 2 
			[i]Okay, Jason is someone who is annoying or poorly regarded, but he’s a messenger rather than a culprit.[/i] 
			*goto jason_choice
		*else
			[i]Is that supposed to mean that he has a bad reputation but is actually a good guy? Or does it mean follow the messenger and proceed in the current direction?[/i]
			*goto jason_choice
		
		*label jason_choice
		Do you start with Jason?
		*choice
			#Yes
				*set investigate_1 "Jason"
				*set investigate_2 "Tonya"
				*set investigate_3 "Zenina"
				*set investigate_4 "Monica"
				*set investigate_5 "Van"
				*goto breakup
			#No
				You try another virtual tea leaf reading. [i]Which person should I start with?[/i] 
				 
				The tea leaves look like a sun. 
				*if soul > 2
					[i]Fame and glory, so start with someone well known.[/i] 
					*goto tonya_start
				*else 
					[i]Does that mean look for someone with no secrets? Or someone who has gotten a lot of attention?[/i]
					*goto tonya_start

				*label tonya_start
				*page_break
				You grab a binder, and skim the records of [b]Tonya S[/b].
				*gosub sub_Tonya_file
				*page_break
				Do you start with Tonya?
				*choice
					#Yes
						*set investigate_1 "Tonya"
						*set investigate_5 "Jason"
						*set investigate_2 "Zenina"
						*set investigate_3 "Monica"
						*set investigate_4 "Van"
						*goto breakup
					#No
						*set investigate_1 "Monica"
						*set investigate_5 "Jason"
						*set investigate_2 "Tonya"
						*set investigate_3 "Zenina"
						*set investigate_4 "Van"
						*page_break
						You grab another binder, and flip through the case file of [b]Monica T[/b]. 
						*gosub sub_Monica_file 

						Looks like Monica will be your first interview of the day.
						*goto breakup
			
	#Decide to save time and plan to do them in random order. It won’t matter as long as you get to everyone before 3pm. 
		*set boss_regard -1
		*set did_random true
		You put the binders in a row, and count out eeny-meenie-miny-moe. Your first investigation will be Jason L.
		*goto breakup		
		
*label sub_Tonya_file
*set read_Tonya true
Your attention is caught by one phrase in particular:
*fake_choice
	#From a peer review: “She’s even cooler in person that she seems online.”
		[i]I wonder how they know her from online?[/i]
		
	#From a manager evaluation: “Excellent customer-facing skills; several successful presentations at customer sites this year.”
		[i]I’m jealous; she sounds like a dynamo.[/i]
		
	#From web browsing log: “News: 0 views, Social media: 0 views, Online dating: 0 views, Forums: 0 views”
		[i]Really? Zero web browsing? I wonder if SEKA doesn’t get data from smart phones.[/i]
		
	#From an incoming email: “I hope you’ll write back, or start posting again! I miss your humor and flair for the ridiculous.”
		[i]I wonder what she used to post about?[/i]

[i]Tonya sounds like she’d be more interesting to talk to...but interesting is not supposed to be our criteria.[/i]
*return

*label sub_Monica_file
*set read_Monica true
Your attention is caught by one phrase in particular:
*fake_choice
	#From a peer review: “Her office is literally stuffed with stuffed animals.”
		[i]I wonder if she has any stuffed bunny rabbits?[/i]
		
	#From a manager evaluation: “She needed extensions on deadlines for two reports. She should focus more on time management.”
		[i]At least she doesn’t have to interview five people before 3pm.[/i]

	#From web browsing log: “News: 10 views, Social media: 100 views, Online dating: 0 views, Forums: 0 views”
		[i]Nothing too unusual there.[/i]
		
	#From an incoming email: “I haven’t received it yet, and wanted to make sure the package wasn’t stolen off my porch.”
		[i]I heard about some petty theft rings following delivery trucks around to steal packages. Wonder why they’re asking her about their package.[/i]

[i]Monica sounds a little flakey. Does that make her more or less suspicious than the others?[/i]
*return
		
*label other_files
*page_break
You pick up the second case file for [b]Zenina B[/b].  
*set read_Zenina true

Your attention is caught by one phrase in particular:
*fake_choice
	#From a peer review: “She’s nice, but sometimes tries to act like my Mom and tell me how to dress.”
		[i]At least your work Mom isn’t telling you to go into the family divination business.[/i]
		
	#From a manager evaluation: “She is eager and willing to learn new software features, and then help teach her coworkers.”
		[i]Maybe Margot should recruit her to join the team.[/i]
		
	#From a web browsing log: “News: 30 views, Social media: 200 views, Online dating: 0 views, Forums: 0 views”
		[i]I wonder if she’s one of those people who posts news articles on Facebook all the time.[/i]
		
	#From an incoming email: “My grandson is still being treated for his allergies and we’re praying he’ll grow out of it.”
		[i]I wonder if they’re trying homeopathic treatments. [/i]

[i]I have no idea if Zenina sounds more or less innocent than Jason.[/i]
*page_break
Who’s next? You pick up the third binder, a case file for [b]Tonya S[/b]. 

*gosub sub_Tonya_file

*page_break
Two more. You pick up the binder for [b]Monica T[/b]. 

*gosub sub_Monica_file

*page_break
Last one. You pick up the final case file for [b]Van O[/b]. 

Your attention is caught by one phrase in particular:
*set read_Van true
*fake_choice
	#From a peer review: “Don’t let him corner you at lunch, or he’ll bore you to death with statistics about politics.”	
		[i]Probably not hard to get him talking, then. But it might be tough to steer the conversation.[/i]
	#From a manager evaluation: “Good attention to documentation, but has a tendency to cover some topics in unnecessary detail.
		[i]Sounds like he probably doesn’t take direction that well.[/i]
	#From web browsing log: “News: 60 views, Social media: 10 views, Online dating: 30 views, Forums: 100 views”
		[i]I wonder what kind of forums he’s on. Probably political stuff.[/i]
	#From an incoming email: “Things are going to go crazy in Indonesia. The government’s just making the situation more unstable every time they try to crack down.”
		[i]I wonder if political analysis is part of his job, or just a serious hobby.[/i]

*page_break
*temp person_choice
*temp total 15
Who do you investigate first?
*gosub pick_order
*set investigate_1 "${person_choice}"

*page_break
Who will be second on your list?
*gosub pick_order
*set investigate_2 "${person_choice}"

*page_break
Who will be third on your list?
*gosub pick_order
*set investigate_3 "${person_choice}"

*page_break
Who will be next to last on your list?
*gosub pick_order
*set investigate_4 "${person_choice}"

*page_break
Looks like your last choice is obvious.
*if total = 1
	*set investigate_5 "Van"
*if total = 2
	*set investigate_5 "Monica"
*if total = 3
	*set investigate_5 "Tonya"
*if total = 4
	*set investigate_5 "Zenina"
*if total = 5
	*set investigate_5 "Jason"
*goto breakup

*label pick_order
*choice
	*disable_reuse #Van O - boring, politics
		*set person_choice "Van"
		*set total -1
		*return
	*disable_reuse #Monica T - late reports, stuffed animals
		*set person_choice "Monica"
		*set total -2
		*return
	*disable_reuse #Tonya S - online persona, go-getter
		*set person_choice "Tonya"
		*set total -3
		*return
	*disable_reuse #Zenina B - helpful, possibly a busybody
		*set person_choice "Zenina"
		*set total -4
		*return
	*disable_reuse #Jason L - slacker gamer
		*set person_choice "Jason"
		*set total -5
		*return

*label breakup
*page_break
Margot and Rene have finished looking through their case files. 

“See you at 3pm,” Margot mutters as she rushes out. 

“Can’t wait to hear how your investigations go. See you later,” Rene chirps, strolling out behind her.
*finish

