*if skip
	*finish
“We’ve been correcting SEKA for over a week now, and we’ve covered some important topics. I’m sure you both remember yesterday’s work tuning SEKA to recognize Ubiquity employees running illicit side businesses. We previously looked at overclaiming on expense reports, harassing coworkers, and a couple types of unfair business practices.”

Rene produces a box of artisanal donuts and offers them around. “What’s on the list for today?” ${ihe} asks.

Have a donut?
*choice
	#Yes, please
		*set resist_colleague -1
		[i]Rene sure knows where to get amazing donuts.[/i]
		*page_break
		*goto margot_explains
	#No thanks
		*goto margot_explains

*label margot_explains
“It’s a biggie,” Margot warns. “We’re targeting cyber security saboteurs, Ubiquity employees who are deliberately aiding attackers in bringing down our computers or stealing company data.”

“Same operating procedures as before?” Rene asks.

“Minor changes. It’s such a serious allegation that we’ll start by just reviewing all the case files with people’s names and identifying information blacked out. That way, we can correct SEKA when it mistakenly flags somebody, without accidentally starting any rumors about them.”

“Any other changes from yesterday?” you ask.

“We’ll budget more time per candidate. Today we’ll only review case files and correct SEKA for the more obvious cases. Tomorrow, we’ll reveal the identities of the most suspicious individual and investigate them in more depth.”

Margot hands each of you a stack of binders. “Document your assessment and reasoning for each of these three people. We’ll talk again at 3pm.”
*page_break
You make it back to your office without dropping any binders, and you take a deep breath before digging into the case records. 

[i]These ten binders cover just three people? Should at least be easier than investigating 10 separate people![/i]

Margot left you a sticky note in the first binder.

“I gave you the easy ones, since you’re new to the job. Person A is unusually interested in finances and investment. Person B is known for temper tantrums. Person C is probably an alcoholic.”
*temp actions_taken
*set actions_taken 0
*temp section_1
*temp section_2
*temp next_part

*page_break
[b]Case History for Person A[/b]

*gosub pick_sections
*page_break
*gosub A_info
*page_break
*gosub A_divination
*page_break
[b]Case History for Person B[/b]

*gosub pick_sections
*page_break
*gosub B_info
*page_break
*gosub B_divination
*page_break
[b]Case History for Person C[/b]

*gosub pick_sections
*page_break
*gosub C_info
*page_break
*gosub C_divination
*page_break
*gosub case_2_summary
*finish

*label pick_sections
Which section of the case history do you read most carefully?
*choice
	#Manager reviews and responses to reviews.
		*set section_1 "annual_reviews"
		*goto secondary
	#Peer ratings.
		*set section_1 "peer_ratings"
		*goto secondary
	#Incoming emails.
		*set section_1 "email"
		*goto secondary
	#Cyber attack trail analysis.
		*set section_1 "cyber_trail"
		*goto secondary
*label secondary
*page_break
Which other section do you focus on?
*choice
	*selectable_if (not(section_1 = "annual_reviews")) #Manager reviews and responses to reviews.
		*set section_2 "annual_reviews"
		*return
	*selectable_if (not(section_1 = "peer_ratings")) #Peer ratings.
		*set section_2 "peer_ratings"
		*return
	*selectable_if (not(section_1 = "email")) #Incoming emails.
		*set section_2 "email"
		*return
	*selectable_if (not(section_1 = "cyber_trail")) #Cyber attack trail analysis.
		*set section_2 "cyber_trail"
		*return

*label A_info
*page_break
*set next_part "A_"&section_1
*gosub {next_part}
*page_break
*set next_part "A_"&section_2
*gosub {next_part}
*return

*label B_info
*page_break
*set next_part "B_"&section_1
*gosub {next_part}
*page_break
*set next_part "B_"&section_2
*gosub {next_part}
*return

*label C_info
*page_break
*set next_part "C_"&section_1
*gosub {next_part}
*page_break
*set next_part "C_"&section_2
*gosub {next_part}
*return

*label A_annual_reviews
The manager reviews look pretty good:

“Person A is a strong performer and well-regarded by his colleagues. His background in finance has been helpful when communicating with Customer M.” 

“Person A should be less hasty about making changes to the software, and spend more time testing for bugs.”

“Person A has solid programming skills, and the team relies on his contributions, but he’s not ready to be a technical lead.” 

It doesn’t look like this person wrote many response to reviews: 

“I’ve enjoyed working in this group and learned a lot. I would like to take on more leadership roles.”
*return

*label A_peer_ratings
Peer ratings:

“Person A is a little young, and thinks he knows a little more than he does, but he’s a solid engineer.” 

“Person A’s got a big ego (e.g. he loves to give out unsolicited investment tips), but he’s also got a good attitude and is willing to learn.” 

“He’s not happy when he has to admit he’s made mistakes, but he has been getting better about it.”
*return

*label A_email
Incoming email: 

“I’m just not a cat fan - they seem like so much trouble.” 

“Congratulations on the new condo! I heard it’s beautiful.” 

“Do you want to meet up for dinner tomorrow?” 

“I have some more questions about those investments you recommended.” 

“I don’t think you’ve been getting all my emails, so I cc’d your web email address.”
*return

*label A_cyber_trail
Cyber trail analysis:

“URL records for Person A include several sites for malware download.” 

“Person A’s activity logs on the server were erased last week.” 

“Person A’s activity logs include downloads of the Project Z archives, a project Person A is not assigned to.”
*return

*label A_divination
It’s going to be hard to figure out the real story behind this would-be investment wiz. You decide to get some help from divination.

The leaves in your Virtual Teacup take the shape of:

*if soul > 3
	A fulcrum, the balance point for a scale. This person faces a temptation, but has not yet made a decision.
	*gosub A_decide
	*return
*elseif (history > 3) or (rumor > 3)
	A coin. This person is attracted to the high life.
	*gosub A_decide
	*return
*else
	*comment presumably your interaction > 3
	A mask. This person is worried about reputation and appearing successful.
	*gosub A_decide
	*return

*label A_decide	
*page_break
What do you decide about Person A?
*choice
	#Investigate further
		You submit a recommendation to further investigate Person A.
		*set A_choice 1
		*return
	#Drop the investigation 
		*set boss_regard -1
		*set A_choice 2
		You submit a recommendation to stop investigation of Person A.
		*return

*label B_annual_reviews
The manager reviews are full of complaints: 

“Person B is on his fourth group in five years. He voluntarily transferred twice, and was re-assigned due to complaints once.” 

“Person B prefers to work outside of core business hours, and so is missing important coordination meetings.” 

“Person B refuses to use common software packages such as Microsoft PowerPoint due to philosophical objections to Microsoft.”

This person wrote quite a few responses: 

“I have been unfairly targeted by managers who spend all their time enforcing useless policies and no time doing real work.” 

“I accomplish twice the work of any of my peers by avoiding ‘required’ meetings and time-wasting small talk.”
*return

*label B_peer_ratings
Peer ratings:

“Person B does best when he can work on tasks that do not require communication with other people, so he doesn’t have a chance to yell at them.” 

“I have never had a personal conversation with Person B, except he did once ask if I could loan him money.” 

“I was warned by his former teammates that Person B likes to blame other people for problems. Loudly.”
*return

*label B_email
Incoming email:

“Send any class reunion notes to the Reunion Chair by the end of the month.” 

“Enroll in a debt management plan now, and reduce your monthly payments by 50%!” 

“I need your status report ASAP.”
*return

*label B_cyber_trail
Cyber trail analysis:

“Malware was installed on Person B’s computer last week by launching an executable file on the desktop, Friday at 1am.” 

“Person B’s account logs on the server showed port scanning activity lasting 12 hours.” 

“Person B logged into the server remotely at 9pm on Sunday.”
*return

*label B_divination
It’s going to be hard to figure out the real story behind this engineer with anger management issues. You decide to get some help from divination.

The leaves in your Virtual Teacup take the shape of:

*if soul > 3
	A mule. This person instinctively resists and opposes.
	*gosub B_decide
	*return
*elseif (interaction > 3) or (rumor > 3)
	A sword. This person is often the center of conflict and strife.
	*gosub B_decide
	*return
*else
	*comment presumably you are > 3 in history
	A shoe. This person moves from place to place, and will not settle down. 
	*gosub B_decide
	*return

*label B_decide	
*page_break
What do you decide about Person B?
*choice
	#Investigate further
		You submit a recommendation to further investigate Person B.
		*set B_choice 1
		*return
	#Drop the investigation 
		*set B_choice 2
		*set boss_regard -1
		You submit a recommendation to stop investigation of Person B.
		*return

*label C_annual_reviews
The manager reviews are mixed: 

“Person C has a solid record over the many years since he came here from EV3, but his performance this year has been uneven.” 

“Person C is well-regarded by colleagues, but several recent unexplained absences put unnecessary pressure on the team to cover for him at the last minute.” 

“Person C mentors the junior staff and establishes a collaborative atmosphere.”

This person responded to some of the reviews: 

“I am deeply sorry for being unreliable, and will not repeat these mistakes.”
*return

*label C_peer_ratings
Peer ratings:

“Person C is friendly and knows the business.” 

“I’m worried that Person C must have something really bad going on outside of work, because he’s been so distracted.” 

“During our last business trip, Person C drank a lot at a dinner with EV3 reps, and missed the morning meetings.” 
*return

*label C_email
Incoming email:

“The custody hearing will be in two months.” 

“Where were you? We thought you were running the workshop yesterday, but when you didn’t show up, F had to take over.” 

“Thank you for volunteering last weekend; we raised $2000 for charity.”
*return

*label C_cyber_trail
Cyber trail analysis:

“Person C disabled the automatic virus checker updates on his computer.” 

“Malware reached Person C’s computer through a flash drive.” 

“The backup server was activated when the primary server went down last week. The backup server did not have updated anti-virus and was infected from Person C’s computer.”
*return

*label C_divination
It’s going to be hard to figure out the real story behind this recruiter with a drinking problem. You decide to get some help from divination.

The leaves in your Virtual Teacup take the shape of:

*if (soul > 3) or (rumor >3)
	A manacle and chain. This person is at the mercy of external influences.
	*gosub C_decide
	*return
*elseif interaction > 3
	A cup lying on its side. This person will disappoint others.
	*gosub C_decide
	*return
*else
	*comment presumably you are > 3 in history
	An egg. This person protects his children. 
	*gosub C_decide
	*return

*label C_decide
*page_break
What do you decide about Person C?
*choice
	#Investigate further
		You submit a recommendation to further investigate Person C.
		*set C_choice 1
		*return
	#Drop the investigation 
		*set boss_regard -1
		*set C_choice 2
		You submit a recommendation to stop investigation of Person C.
		*return

*label case_2_summary
*page_break
Who do you think is the most likely to be deliberately sabotaging Ubiquity?
*choice
	#Person A - would-be investment wiz
		*set case_2_insider "A"
		*gosub accident
		*return
	#Person B - engineer with anger management issues
		*set case_2_insider "B"
		*gosub accident
		*return
	#Person C - recruiter with a drinking problem
		*set case_2_insider "C"
		*gosub accident
		*return
	#No one
		*gosub accident
		*return

*label accident
*page_break
Who do you think is the most likely to have accidentally caused a security breach?
*choice
	*selectable_if (not(case_2_insider = "A")) #Person A - would-be investment wiz
		*set case_2_innocent "A"
		*gosub end_report
		*return
	*selectable_if (not(case_2_insider = "B")) #Person B - engineer with anger management issues
		*set case_2_innocent "B"
		*gosub end_report
		*return
	*selectable_if (not(case_2_insider = "C")) #Person C - recruiter with a drinking problem
		*set case_2_innocent "C"
		*gosub end_report
		*return
	#No one
		*gosub end_report
		*return

*label end_report
*page_break
You finish writing up short reports summarizing your assessment of each person’s behavior, giving yourself a pat on the back for your time management triumph.
*return